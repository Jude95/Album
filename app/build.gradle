apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

Properties props = new Properties()
props.load(new FileInputStream(file("../config/signing.properties")))
props.load(new FileInputStream(file("../config/key.properties")))

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.jude.album"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 9
        versionName "1.9"
        jackOptions{
            enabled false
        }
        manifestPlaceholders = [
                UMENG_APPKEY    : props['UMENG_APPKEY'],
                SMS_MOB_APPKEY : props['SMS_MOB_APPKEY'],
                SMS_MOB_APPSECRET : props['SMS_MOB_APPSECRET']
        ]
    }

    signingConfigs {
        release{
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.outputFile != null && output.outputFile.name.endsWith('.apk')
                            &&'release'.equals(variant.buildType.name)) {
                        def apkFile = new File(
                                output.outputFile.getParent(),
                                "Album_${variant.flavorName}_v${variant.versionName}_${buildTime()}.apk")
                        output.outputFile = apkFile
                    }
                }
            }
            ndk {
                abiFilters "armeabi"
            }
        }
        debug {
            signingConfig signingConfigs.release
            ndk {
                abiFilters "armeabi"
            }
        }
    }

    compileOptions {//配置java1.8环境
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        incremental = true;
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.xml'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.properties'
    }
    lintOptions {
          abortOnError false
    }
}
def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'
    //Image
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'jp.wasabeef:glide-transformations:2.0.1'
    compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.3.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    //Net
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.qiniu:qiniu-android-sdk:7.0.1'
    //Rx
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile('com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar') {
        exclude module: 'rxbinding'
    }
    //UI
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.afollestad.material-dialogs:core:0.8.5.9'
    compile 'com.github.arimorty:floatingsearchview:2.0.1'
    compile 'com.github.chrisbanes:PhotoView:1.2.6'
    compile 'cn.bingoogolapple:bga-flowlayout:1.0.0@aar'
    //Dagger
    compile 'com.google.dagger:dagger:2.4'
    apt 'com.google.dagger:dagger-compiler:2.4'
    //butterknife
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    //Jude Family
    compile('com.jude:beam:2.9.4') {
        exclude module: 'easyrecyclerview'
        exclude module: 'rxandroid'
    }
    compile 'com.jude:utils:1.2.1'
    compile 'com.jude:rollviewpager:1.3.1'
    compile 'com.jude:tagview:2.1.5'
    compile 'com.jude:fitsystemwindowlayout:2.1.4'
    compile 'com.jude:swipebackhelper:3.1.0'
    compile 'com.jude:easyrecyclerview:4.0.2'
    compile 'com.jude:smssdk_mob:1.1.0'
    compile 'com.jude:imageprovider:2.1.0'
    //Debug
    compile 'com.facebook.stetho:stetho:1.3.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.0'
    compile 'com.github.romainguy:ViewServer:master'
    //Third
    compile 'com.umeng.analytics:analytics:latest.integration'
}
